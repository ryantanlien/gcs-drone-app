plugins {
    id 'org.springframework.boot' version '3.0.0-M2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

group = 'dvd.gcs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

//Specify entry point for application
application {
    mainClass = 'dvd.gcs.app.ThickDemoApplication'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation group: 'org.pf4j', name: 'pf4j', version: '3.6.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.3'
}

//Specifies JavaFX version
javafx {
    version = "17.0.1"
    modules = [ 'javafx.controls', 'javafx.graphics', 'javafx.swing', 'javafx.base', 'javafx.fxml' ]
}

//Specify Test Framework
tasks.named('test') {
    useJUnitPlatform()
}

task uberjar(type: Jar, dependsOn: ['compileJava']) {
    zip64 true
    from configurations.runtimeClasspath.asFileTree.files.collect {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        zipTree(it)
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': application.mainClass
    }

    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.resources)
}

//Specify tasks
tasks.register("printSourceSetInformation") {
    doLast {
        sourceSets.each { srcSet -> {
            println "[" + srcSet.name + "]"
            print "--> Source directories: " + srcSet.allJava.srcDirs + "\n"
            print "--> Output directories: " + srcSet.output.classesDirs.files + "\n"
            print "--> Compile classpath:\n"
            srcSet.compileClasspath.files.each {
                println " " + it.path + "\n"
            }
            println ""
        }
        }
    }
}